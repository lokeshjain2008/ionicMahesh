// Functions

isSelfData(id){ auth != null && auth.uid == id}
isLoggedIn() {  auth != null}
isFather(){
  auth!=null && prior(root).child('users').child(auth.uid).child('role').val() == 'father';
}

// Data Type
type Menu{
	name: String,
  price: Number,
	desc: String,
  available: Boolean

	validate(){
    this.name != null &&
    this.price >0 &&
    this.desc.length <=140;
  }

}

path /menu {
  read(){
    isLoggedIn();
  }
  write(){
    isFather();
  }
}
// All individual Posts are writable by anyone.
path /menu/{id} is Menu {
  index(){
    ['available']
  }
}


// can be defined later
type User {
  email: String,
  profileImageURL: String,
  displayName: String,
  userId: String,

}

path /users {
  read(){
    isFather()
  }
}

path /users/{userId} is User{
	read(){
    isSelfData(userId)
  }
	write(){
    isSelfData(userId);
  }
}

// canteen
type Canteen {
  name:  String;
  owner: String;
  accountNumber: Number;
  desc: String;
}

path /canteen  is Canteen{
  read(){
    true;
  }
  write(){
    isFather();
  }

}

//order element
type Order{
  items: Menu[],
  amount: Number,
  status: Number,
}

type UserOrder{
  amount: Number,
  orders: Order[]
}

path /user-orders {
  read(){
    isFather()
  }
  write(){
    isFather()
  }
}

path /user-orders/{userId} is UserOrder{
  read(){
    isSelfData($userId)
  }
  create(){
    isSelfData($userId)
  }
  update(){
    isSelfData($userId) && prior(this).status!=2
  }
  delete(){
    isSelfData($userId) && prior(this).status!=2
  }
  //index(){ //how
  //  ['orders/$key/order/status']
  //}
}




path /tables {
  read(){
    isLoggedIn();
  }
  write(){
    isFather();
  }
}

path /reservations {
  read(){
    isLoggedIn();
  }
  write(){
    auth!=null && ( auth.uid == this.child('userId').val() || prior(root).child('users').child(auth.uid).child('role').val() == 'father');
  }
}


//UserId is need to reach the actual data and update
path /current-orders {
  read() {
    isFather();
  }
  write(){
    isLoggedIn();
  }
}


//Test data

path /textItems {
  read() {
    true;
  }
}



// See this https://github.com/firebase/bolt/blob/master/docs/language.md
//
// bolt is an alias for firebase-bolt < rules.bolt > rules.json

// Important rules to understand
// 1. Either you can have `write` or all other `create, update, delete` to gain more control
// 2. Exists() is not there in the bolt
// 3. We can put validation related thing `validate` in type definition of rule definition.
// 4. For the timestamp for the created and update user `Firebase.ServerValue.TIMESTAMP` in you code not here.
//    For more http://stackoverflow.com/questions/34266879/can-i-created-date-property-if-user-not-send
// 5. There is difference in the `prior` with root. and root i.e [prior(root) vs root]
// 		Note this,The reason is subtle (not obvious): To support multi-location updates, you need to be able to write
// 		rules that will refer to the "new" value of the date. The standard in Bolt is that in write and validation rules, this and root references are newData values (in read rules, this and root are data - old/existing - values).
// 6. More use of prior https://github.com/firebase/bolt/issues/35
// 7. Related to Timestamp. there is more the `bolt` https://github.com/firebase/bolt/blob/master/docs/guide.md
//
//

